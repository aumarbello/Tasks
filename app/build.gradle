buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'
        classpath 'me.tatarka:gradle-retrolambda:3.6.1'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion rootProject.ext.compiledSdk
    buildToolsVersion rootProject.ext.buildToolVersion
    defaultConfig {
        applicationId "com.example.ahmed.simpDo"
        minSdkVersion rootProject.ext.minimumSdk
        targetSdkVersion rootProject.ext.targetSdk
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dexOptions {
        javaMaxHeapSize "12g"
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    packagingOptions {
        pickFirst 'licenses/*'
        pickFirst 'META-INF/rxjava.properties'
        pickFirst 'org/cyberneko/html/res/*'
        pickFirst 'META-INF/plexus/*'
        pickFirst 'org.codehaus/plexus/*'
        pickFirst 'org/codehaus/plexus/*'
        pickFirst 'META-INF/NOTICE'
        pickFirst 'META-INF/LICENSE'
        pickFirst 'META-INF/DEPENDENCIES'
        pickFirst 'META-INF/notice.txt'
        pickFirst 'META-INF/license.txt'
        pickFirst 'META-INF/dependencies.txt'
        pickFirst 'META-INF/LGPL2.1'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    //Library definitions
    def supportLib = rootProject.ext.supportDependencies
    def rxDependencies = rootProject.ext.rxDependencies
    def daggerDependencies = rootProject.ext.daggerDependencies
    def butterKnifeDependencies = rootProject.ext.butterKnifeDependencies
    def jUnit = rootProject.ext.junitDependency
    def mockito = rootProject.ext.mockitoDependency
    def roboLectric = rootProject.ext.roboLectricDependency
    def roboShadow = rootProject.ext.roboLectricShadowsDependency
    def espresso = rootProject.ext.espressoDependency
    def espressoExclude = rootProject.ext.espressoExcludeGroups
    def sectionedAdapter = rootProject.ext.sectionedAdapter
    def colorPreference = rootProject.ext.colorPreference

    //supportLib
    compile supportLib.appCompat
    compile supportLib.support
    compile supportLib.design
    compile supportLib.cardView
    compile supportLib.recyclerView

    //rxJava
    compile rxDependencies.rxJava
    compile rxDependencies.rxAndroid

    //dagger
    compile daggerDependencies.dagger
    annotationProcessor daggerDependencies.daggerApt

    //butterKnife
    compile butterKnifeDependencies.butterknife
    annotationProcessor butterKnifeDependencies.butterknifeApt

    //sectionAdapter
    compile sectionedAdapter

    //colorPreference
    compile colorPreference

    //others
    compile 'com.squareup:android-times-square:1.6.5@aar'

    //tests
    testCompile jUnit
    testCompile mockito
    testCompile roboLectric
    testCompile roboShadow


    androidTestCompile(espresso, {
        exclude group: espressoExclude.group1, module: espressoExclude.module1
        exclude group: espressoExclude.group2, module: espressoExclude.module2
        exclude group: espressoExclude.group3, module: espressoExclude.module3
        exclude group: espressoExclude.group4, module: espressoExclude.module4
        exclude group: espressoExclude.group5, module: espressoExclude.module5
    })

    configurations.all {
        resolutionStrategy.force "com.android.support:support-annotations:$supportLibraryVersion"
        resolutionStrategy.force "com.google.guava:guava:20.0-rc1"
    }
    compile 'javax.inject:javax.inject:1'
    provided 'javax.annotation:jsr250-api:1.0'
}